@echo off

rem Counter-Strike: Source (CS:S) content is not included by default in Garry's Mod.
rem CS:S content is available for free with CS:S Dedicated Server through the Steam Console Client (SteamCMD).

rem See the Valve developer wiki for more information on SteamCMD:
rem https://developer.valvesoftware.com/wiki/SteamCMD
rem https://developer.valvesoftware.com/wiki/Command_Line_Options#SteamCMD
rem https://developer.valvesoftware.com/wiki/Steam_Application_IDs

rem See Microsoft Learn for more information on Windows commands:
rem https://learn.microsoft.com/en-us/windows-server/administration/windows-commands/windows-commands

set varSteamCMD= ^
if (Test-Path -Path %temp%\steamcmd\steamcmd.exe -PathType leaf) { ^
} elseif (Test-Path -Path %temp%\steamcmd.zip -PathType leaf) { ^
	Expand-Archive -LiteralPath %temp%\steamcmd.zip -DestinationPath %temp%\steamcmd ^
} elseif (Test-Path -Path %temp%\steamcmd.exe -PathType leaf) { ^
	if (-not (Test-Path -Path %temp%\steamcmd -PathType container)) {New-Item -ItemType directory -Path %temp%\steamcmd} ^
	Move-Item -Path %temp%\steamcmd.exe -Destination %temp%\steamcmd\steamcmd.exe ^
} else { ^
	(New-Object Net.WebClient).DownloadFile('https://steamcdn-a.akamaihd.net/client/installer/steamcmd.zip','%temp%\steamcmd.zip'); ^
	Expand-Archive -LiteralPath %temp%\steamcmd.zip -DestinationPath %temp%\steamcmd ^
}
powershell -command "%varSteamCMD%"

set "varSelectDir="(new-object -COM 'Shell.Application').BrowseForFolder(0,'Please select DRIVE:\Program Files (x86)\Steam\steamapps\common\GarrysMod.',0,0).self.path""
for /f "usebackq delims=" %%I in (`powershell -command %varSelectDir%`) do set "varClientDir=%%I"

start /b /wait %temp%\steamcmd\steamcmd.exe +login anonymous +force_install_dir %temp%\steamcmd\cstrike +app_update 232330 validate +quit
start /b /wait robocopy "%temp%\steamcmd\cstrike\cstrike" "%varClientDir%\garrysmod\addons\cstrike" "*.vpk"

wmic process where "ExecutablePath='%varClientDir:\=\\%\\hl2.exe'" call terminate
> "%varClientDir%\garrysmod\cfg\mount.cfg" (
	echo "mountcfg"
	echo {
	echo "cstrike"  "%varClientDir%\garrysmod\addons\cstrike"
	echo }
)
> "%varClientDir%\garrysmod\cfg\mountdepots.txt" (
	echo "gamedepotsystem"
	echo {
	echo "cstrike"  "1"
	echo }
)

if exist %temp%\steamcmd.zip del /q %temp%\steamcmd.zip
if exist %temp%\steamcmd\ rmdir /s /q %temp%\steamcmd